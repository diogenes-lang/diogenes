grammar it.unica.co2.Contracts with org.eclipse.xtext.common.Terminals
//grammar it.unica.co2.Contracts with org.eclipse.xtext.xbase.Xbase

generate contracts "http://www.unica.it/co2/Contracts"

ContractGrammar :
	contracts+=ContractDefinition*
;

ContractDefinition:
	{ContractDefinition} "contract" name=ID "{" 
		contract=(Contract)? 
	"}" 
;

Contract:
	IntSum | 
	ExtSum | 
	Recursion |
	{EmptyContract} value="0" |
	"(" Contract ")"
;

IntSum:
	actions+=IntAction ("(+)" actions+=IntAction)*
;

ExtSum:
	actions+=ExtAction ( "+"  actions+=ExtAction)*
;

Recursion:
	"rec" name=ID "." body=NextContract
;

NextContract returns Contract:
	IntAction |
	ExtAction |
	{EmptyContract} value="0" |
	"(" Contract ")"
;

IntAction:
	actionName=ID "!" (type=ActionType)? next=AbstractNextContract?
;

ExtAction:
	actionName=ID "?" (type=ActionType)? next=AbstractNextContract?
;

AbstractNextContract:
	"." (
		nextContract=NextContract |
		recursionReference=[Recursion] |
		"&" contractReference=[ContractDefinition]
	)
;

ActionType: 
	{UnitActionType} "unit" |
	{IntActionType} "int" |
	{StringActionType} "string"
;
