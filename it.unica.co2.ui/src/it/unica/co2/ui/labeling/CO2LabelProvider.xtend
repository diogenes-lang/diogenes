/*
 * generated by Xtext
 */
package it.unica.co2.ui.labeling

import com.google.inject.Inject
import it.unica.co2.co2.DelimitedProcess
import it.unica.co2.co2.DoInput
import it.unica.co2.co2.DoOutput
import it.unica.co2.co2.ElseStatement
import it.unica.co2.co2.IfThenElse
import it.unica.co2.co2.ParallelProcesses
import it.unica.co2.co2.ProcessDefinition
import it.unica.co2.co2.ProcessReference
import it.unica.co2.co2.Sum
import it.unica.co2.co2.Tau
import it.unica.co2.co2.Tell
import it.unica.co2.co2.ThenStatement
import org.eclipse.emf.edit.ui.provider.AdapterFactoryLabelProvider

/**
 * Provides labels for EObjects.
 * 
 * See https://www.eclipse.org/Xtext/documentation/304_ide_concepts.html#label-provider
 */
class CO2LabelProvider extends ContractsLabelProvider {

	@Inject
	new(AdapterFactoryLabelProvider delegate) {
		super(delegate);
	}

	def image(ProcessDefinition ele) {
		'process.png'
	}
	
	
	
	def String text(ProcessDefinition ele) {
		var name = ele.name?: '<anonymous>'
		if (ele.freeNames.length!=0)
			'''«name» «ele.freeNames.join("(", ",", ")", [x|x])»'''
		else
			name
	}
	
	def text(DelimitedProcess ele) {
		ele.freeNames.join(" ", [ String x | "("+x+")"])
	}
	
	def text(Sum ele) {
		"+"
	}
	
	def text(ParallelProcesses ele) {
		"|"
	}
	
	def text(Tell ele) {
		if (ele.contractReference!=null)
			"tell " + ele.session + " [" + ele.contractReference.name + "]"
		else
			"tell " + ele.session
	}
	
	def text(DoInput ele) {
		"do "+ ele.session + " " + ele.actionName + "?"
	}
	
	def text(DoOutput ele) {
		"do "+ ele.session + " " + ele.actionName + "!"
	}
	
	def text(Tau ele) {
		"\u03c4"
	}
	
	def text(IfThenElse ele) {
		"if"
	}
	
	def text(ThenStatement ele) {
		"then"
	}
	
	def text(ElseStatement ele) {
		"else"
	}
	
	def String text(ProcessReference ele) {
		var name = ele.reference.name?: '<anonymous>'
		if (ele.variables.length!=0)
			'''«name» «ele.variables.join("(", ",", ")", [x|x])»'''
		else
			name
	}
}