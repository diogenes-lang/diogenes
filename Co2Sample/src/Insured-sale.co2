/*
 *  Insured-sale
 */
system it.unica.co2.test.InsuredSale

// list of processes will be checked for honesty
honesty PA


contract PIPPO  {
	pippo! int
}

contract CA  {
	order ? int . (amount! int . pay ? (+) abort!.PIPPO)
}

contract CI {
	reqI ! unit . (okI ? + abortI ?)
}


process PA {
	(x:session) (
		tell x CA . (
			do x order ? n:int . (if n>50 then PAY(x) else INS(x))
		) 
	)
}

process PAY (x:session) {
    do x amount ! . do x pay ? + do x abort !
}
   
process INS (x:session) {
	(y:session) tell y CI . (
        do y reqI ! . ( 
              do y okI ? . PAY(x)
            + do y abortI ? . do x abort !
            + t . ( do x abort ! | do y okI ? + do y abortI ? ) 
        )
        + t . (
        	do x abort ! | 
			do y reqI ! . (
				do y okI ? + do y abortI ?
			)
        ) 
    )
}

